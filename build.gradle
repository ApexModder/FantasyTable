buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		// maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: fg_version, changing: true
		// classpath "org.spongepowered:mixingradle:${mixin_version}"
	}
}

plugins {
	id 'com.github.johnrengelman.shadow' version "${jr_shadow_version}"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
// apply plugin: 'org.spongepowered.mixin'

version = "${mod_version}"
group = "${apex_package}.${mod_id}"
archivesBaseName = "${mod_name}-${mc_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
	shade
}

minecraft {
	mappings channel: 'official', version: "${mc_version}"
	// accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run/client')

			// property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			// property 'forge.logging.console.level', 'debug'

			// property 'mixin.env.disableRefMap', 'true'
			// args '-mixin.config=' + mod_id + '.mixins.json'

			mods {
				DiceMod {
					source sourceSets.main
				}
			}
		}

		clientAuthed {
			parent runs.client

			// args '-mixin.config=' + mod_id + '.mixins.json'

			if(project.hasProperty('mc_uuid')) {
				args '--uuid', project.getProperty('mc_uuid');
			}

			if(project.hasProperty('mc_username')) {
				args '--username', project.getProperty('mc_username');
			}

			if(project.hasProperty('mc_accessToken')) {
				args '--accessToken', project.getProperty('mc_accessToken');
			}
		}

		server {
			workingDirectory project.file('run/server')

			// property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			// property 'forge.logging.console.level', 'debug'

			// property 'mixin.env.disableRefMap', 'true'
			// args '-mixin.config=' + mod_id + '.mixins.json'

			args 'nogui'

			mods {
				DiceMod {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run/data')

			// property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			// property 'forge.logging.console.level', 'debug'

			// property 'mixin.env.disableRefMap', 'true'
			// args '-mixin.config=' + mod_id + '.mixins.json'

			args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

			mods {
				DiceMod {
					source sourceSets.main
				}
			}
		}
	}
}

/*mixin {
	add sourceSets.main, "${mod_id}.refmap.json"
}*/

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

repositories {
	mavenLocal() // Local Deps
	maven { url 'https://dvs1.progwml6.com/files/maven' } // JEI
	maven { url 'https://maven.tterrag.com/' } // TOP
}

dependencies {
	minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

	// JEI
	compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
	runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")

	// TOP
	compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${mc_version_short}:${mc_version_short}-${top_version}:api")
	runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${mc_version_short}:${mc_version_short}-${top_version}")

	// Registrate
	compileOnly fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}")
	runtimeOnly fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}")
	shade "com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}"

	if(project.hasProperty('apex_local_deps') && project.getProperty('apex_local_deps')) {
		// runtimeOnly fg.deobf("${apex_package}:devworld:${mc_version}-${devworld_version}")
		// runtimeOnly fg.deobf("${apex_package}:smoothboot:${mc_version}-${smoothboot_version}")
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

task deobfJar(type: Jar) {
	classifier = 'deobf'
	from sourceSets.main.output
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
	archives shadowJar
}

jar {
	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir

	manifest {
		attributes([
				'Specification-Title': "${mod_name}",
				'Specification-Vendor': "${mod_author}",
				'Specification-Version': '1',
				'Implementation-Title': "${mod_name}",
				'Implementation-Version': "${mod_version}",
				'Implementation-Vendor' : "${mod_name}",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
				// 'MixinConfigs': "${mod_id}.mixins.json"
		])
	}
}

shadowJar {
	configurations = [ project.configurations.shade ]
	relocate 'com.tterrag.registrate', "${project.group}.repack.com.tterrag.registrate"
	classifier = 'fat'
}

jar.finalizedBy('reobfJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "${apex_package}"
			artifactId = "${mod_id}"
			version = "${mc_version}-${mod_version}"

			artifact jar
			artifact sourcesJar
			artifact deobfJar
			artifact shadowJar
		}
	}
	repositories {
		mavenLocal()
	}
}